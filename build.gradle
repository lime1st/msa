plugins {
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'org.springframework.boot' version '3.4.4' apply false
}

ext {
    springBootVersion = '3.4.4'
    set('springCloudVersion', "2024.0.1")
    mapstructVersion = "1.6.3"
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    group = 'msa.lime1st'
    version = '1.0.0'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

        implementation 'org.springframework.boot:spring-boot-starter-webflux'

        // mapstruct
        implementation "org.mapstruct:mapstruct:${mapstructVersion}"
        compileOnly "org.mapstruct:mapstruct-processor:${mapstructVersion}"
        annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
        testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.projectreactor:reactor-test'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }

    // ✅ Spring Boot 플러그인이 적용된 모듈에서만 bootBuildImage 설정
    afterEvaluate { project ->
        if (project.plugins.hasPlugin("org.springframework.boot")) {
            project.tasks.named("bootBuildImage") {
                imageName = "${project.name}"
                environment = ["BP_JVM_VERSION": "17.*"]
            }
        }
    }
}

// 컨테이너 이미지 빌드 동시 실행
tasks.register("buildAllImages") {
    group = "build"
    description = "Build bootBuildImage for all Spring Boot subprojects"

    dependsOn subprojects
            .findAll { it.plugins.hasPlugin("org.springframework.boot") }
            .collect { it.tasks.named("bootBuildImage") }
}
